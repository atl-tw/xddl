buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.1.0'
    }
}
plugins {
    id 'com.github.hierynomus.license' version "0.15.0"
    id 'com.diffplug.gradle.spotless' version '3.23.1'
}

group 'net.kebernet'
version '1.0-SNAPSHOT'


subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'com.diffplug.gradle.spotless'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9.1'
        compileOnly 'org.projectlombok:lombok:1.18.8'
        annotationProcessor 'org.projectlombok:lombok:1.18.8'
        testImplementation 'com.google.truth:truth:1.0'
        testImplementation 'junit:junit:4.12'
    }

    spotless {
        groovy {
            excludeJava()
            paddedCell()
        }
        java {
            googleJavaFormat()
        }
        kotlin {
            ktlint().userData(['indent_size': '4', 'continuation_indent_size': '4'])
        }
    }

    dependencyCheck {
        suppressionFile = rootProject.file("src/etc/suppression.xml")
        format = 'ALL'
    }


    license {
        header rootProject.file('src/etc/HEADER')
        strictCheck true
        mapping("java", "SLASHSTAR_STYLE")
        exclude "**/*.json"
    }



    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories =  files(sourceSets.main.output)
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    check.dependsOn dependencyCheckAnalyze
    check.dependsOn licenseFormat
    check.finalizedBy jacocoTestReport
}
