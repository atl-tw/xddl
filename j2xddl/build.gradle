plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

apply plugin: 'application'

dependencies {
    api project(":xddl-core")
    implementation 'com.beust:jcommander:1.72'
    implementation 'org.hibernate:hibernate-validator:6.0.17.Final'
    implementation 'io.swagger.core.v3:swagger-annotations:2.0.1'
}


application {
    mainClassName = 'net.kebernet.xddl.j2xddl.Main'
    startScripts.classpath += files('plugins/*')
}

publishing {
    publications {
        dist(MavenPublication) {
            groupId project.group
            artifactId project.name
            version rootProject.version
            artifact distZip {
                classifier "distribution"
            }
        }
    }
}

if (project.hasProperty("kebernet_bintray")) {
    bintray {
        user = getProperty("kebernet_bintray")
        key = getProperty("kebernet_bintray_api")
        pkg {
            publications += 'dist'
        }
    }
}


import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
}
tasks.build.dependsOn tasks.shadowJar
tasks.shadowJar.dependsOn tasks.relocateShadowJar